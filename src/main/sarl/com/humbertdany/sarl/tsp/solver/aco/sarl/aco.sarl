package com.humbertdany.sarl.tsp.solver.aco.sarl

import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import io.sarl.core.Logging
import com.humbertdany.sarl.tsp.solver.aco.AntColonyTspSolver
import io.sarl.core.DefaultContextInteractions
import com.humbertdany.sarl.tsp.solver.aco.params.AcoParameters
import io.sarl.core.Schedules
import io.sarl.core.Behaviors
import com.humbertdany.sarl.tsp.solver.generic.ALauncher
import com.humbertdany.sarl.tsp.solver.generic.NewGraphState 
import com.humbertdany.sarl.tsp.tspgraph.TspGraph
import com.humbertdany.sarl.tsp.tspgraph.TspVertex
import java.util.List
import java.util.ArrayList 
import io.sarl.lang.core.Address
import io.sarl.util.Scopes 
import com.humbertdany.sarl.tsp.core.graph.Edge
import com.humbertdany.sarl.tsp.tspgraph.VertexInfo
import java.util.Random
import com.humbertdany.sarl.tsp.solver.aco.utils.AcoTspEdgeData
import com.humbertdany.sarl.tsp.solver.generic.StopSolvingEvent
import com.humbertdany.sarl.tsp.solver.generic.NewBestPath
import java.util.concurrent.ConcurrentHashMap
import org.apache.commons.math3.util.FastMath
import com.humbertdany.sarl.tsp.solver.aco.listener.SarlAcoListener

/**
 * Start the solving 
 * Used as first sent and should loop
 */
event NewTick {
	var i : int = 0; 
}

event TriggerTestingEvent {
	var isTesting : boolean;
}

event AntDiedEvent {
	 
}

event NewTspProblemParameters {
	var params : AcoParameters 
} 

event FourmiFinishedWorking {
	var totalCost : double;
	var flow : List<TspVertex>;
}

event InternalBehaviorInitialize {
	
}

event NewTspGraphFourmiliere {
	var graph : TspGraph
}

event FourmiAskDieEvent {
	
}

event FourmiFinishedRaceEvent {
	
}

event AntTickOver {
	var vertex : TspVertex
}

event AntReadyForWork {
	
}
 
event ProcessControllerEvent {
	 
}

event SarlAcoProcessUpdate {
	var obs : SarlAcoListener
}

/**
 * Handling all the stuff about getting parameters 
 */
abstract behavior AcoControllerBehavior {
	
	/** 
	 * The TspGraph currently solving
	 */
	var graph : TspGraph = null;
	
	var ready : boolean = false;
	/** 
	 * Store if the behavior is in testing mode, or not.
	 * This will generally be used to trigger logging
	 */
	var testing : boolean = false;
	
	/** 
	 * Store the parameters available 
	 * for the solver
	 * (Number of ants, etc.)
	 */
	var params : AcoParameters;

	on NewTspProblemParameters { 
		if (occurrence.params != null) {
			params = occurrence.params;
			ready = graph != null && params != null
			defineFourmiliereParams
		}
	}

	on NewGraphState {
		if (occurrence.graph != null) {
			graph = occurrence.graph;
			ready = graph != null && params != null
			defineFourmiliereParams
		}
	}
	
	on TriggerTestingEvent {
		testing = occurrence.isTesting
	}
	
	abstract def defineFourmiliereParams;
	
}

/**
 * The Agent that register this behavior 
 * will watch the graph and check if a solution
 * is available
 */
behavior SolutionObserverBehavior extends AcoControllerBehavior {

	uses DefaultContextInteractions, Logging, Schedules
	
	var visitedMap : ConcurrentHashMap<Address, List<String>> = new ConcurrentHashMap();
	
	var bestCostSoFar : Double = -1.0; // Impossible cost at the beginning
	var bestWaySoFar : List<TspVertex> = new ArrayList();

	on NewGraphState {
		visitedMap = new ConcurrentHashMap();
		bestCostSoFar = -1.0;
		bestWaySoFar = new ArrayList();
	}
	
	on FourmiFinishedWorking {
		if (bestCostSoFar == -1.0) {
			bestCostSoFar = occurrence.totalCost;
			bestWaySoFar = occurrence.flow
			newBest
		} else if (occurrence.totalCost < bestCostSoFar) {
			bestCostSoFar = occurrence.totalCost;
			bestWaySoFar = occurrence.flow
			newBest
		}
	}
	
	def newBest {
		var strRes : StringBuilder = new StringBuilder;
		for (e : bestWaySoFar){
			strRes.append(e.name).append(" > ")
		}
		var evt = new NewBestPath;
		evt.flow = bestWaySoFar
		evt.cost = bestCostSoFar
		emit(evt)
	}

	on AntTickOver {
		var addr = occurrence.source;
		var vertex = occurrence.vertex;
		if (visitedMap.get(addr) == null) {
			visitedMap.put(addr, new ArrayList())
		}
		var visitedList = visitedMap.get(addr)
		if (visitedList != null) {
			if (!visitedList.contains(vertex.name)) {
				visitedList.add(vertex.name)
				if (visitedList.size == graph.verticies.size) {
					emit(new FourmiFinishedRaceEvent, Scopes.addresses(occurrence.source));
					in(100)[
						visitedMap.remove(occurrence.source)
					]
				}
			}
		}
	}

	override protected defineFourmiliereParams {
		// Nothing has to be done for the observer, already up to date
	}

	// TESTS
	var tickCounted : int = 0;

	on NewTick [testing] {
		tickCounted++
		var sb = new StringBuilder
		sb.append("\n").append("\nTick nÂ° ").append(tickCounted).append(", \napproximative ants number = ").append(
			visitedMap.size).append("\ncurrent best cost = ").append(bestCostSoFar).append("\n");
		info(sb.toString);
	}

}

/**
 * The Agent that register this behavior 
 * will handle the Fourmiliere, and so the solving process
 */
behavior FourmiliereBehavior extends AcoControllerBehavior {

	uses DefaultContextInteractions
	
	var antsCount : int = 0;
	var desiredAntsCount : int = 0;

	var isProcessing : boolean = false;

	/**
	 * Here we are checking the number of ants and if we need to kill some
	 * or to spawn some
	 */
	on ProcessControllerEvent [isFromMe(occurrence) && ready && !isProcessing] {
		isProcessing = true;
		if (antsCount != desiredAntsCount) {
			// Not enough fourmi, spawning some to get the count
			if (antsCount < desiredAntsCount) {
				for (var i = 0; i < desiredAntsCount - antsCount; i++) {
					spawnAnt
				}
			} else {
				killAllAnts
			}
		}
		isProcessing = false;
	}

	def spawnAnt {
		spawn(Fourmi, graph.rootVertex, params)
		antsCount++;
	}

	on AntDiedEvent [!isFromMe(occurrence)] {
		antsCount--;
	}

	on NewGraphState {
		killAllAnts 
	}

	def killAllAnts {
		emit(new FourmiAskDieEvent)
	}
	
	/** 
	 * Define the optimal desired fourmi count
	 */
	override protected defineFourmiliereParams {
		if (ready) {
			var mapSize = graph.verticies.size;
			var s = params.antsNumber.toLowerCase;
			if (s.equals("few")) {
				desiredAntsCount = mapSize * 5 // - Ants /Vertex
			} else if (s.equals("alot")) {
				desiredAntsCount = mapSize * 50 // - Ants / Vertex
			} else {
				desiredAntsCount = mapSize * 10 // - Ants / Vertex
			}
			if (desiredAntsCount < 30) {
				desiredAntsCount = 30;
			} 
			// TODO params checker ant number faked
			// Performance problem, maybe linked to multi agent threading killing my PC?
			// Tested the same solver in full Java with Thread Poll, not performance issue?
			// Maybe the approach 1 Agent = 1 Ant is not the best option? Can Janus handle 100k agents?
			// Dev on intel i5, 150 Ant is the max that I could handle without getting lags
			if (desiredAntsCount > 150) {
				desiredAntsCount = 150;
			}
		}
	}

}

/**
 * The Agent that register this behavior 
 * will handle the Ants lifecycle (ticks)
 */
behavior TickSyncronizer extends AcoControllerBehavior {
	
	uses DefaultContextInteractions, Schedules
	
	var lastTickEmitted : long = System.currentTimeMillis();

	var isProcessing : boolean = false;
	
	/**
	 * Store TRUE if the Ant has done working,
	 * FALSE otherwise
	 */
	var workersMap : ConcurrentHashMap<Address, Boolean> = new ConcurrentHashMap();

	on ProcessControllerEvent [isFromMe(occurrence) && !isProcessing && ready && workersMap != null] {
		isProcessing = true;
		var allDone = true;
		for (e : workersMap.values) {
			if (e == false) {
				allDone = false;
			}
		}
		if (allDone) {
			var currentMs = System.currentTimeMillis()
			var timeBetweenLastTick = currentMs - lastTickEmitted;
			for (e : workersMap.entrySet) {
				e.value = false;
			}
			if (timeBetweenLastTick < params.msBetweenTick) {
				in(params.msBetweenTick - timeBetweenLastTick) [
					fireNewTick
				]
			} else {
				fireNewTick
			}
		} 
		isProcessing = false;
	}

	on AntTickOver [!isFromMe(occurrence)] {
		workersMap.replace(occurrence.source, true)
	}

	on AntReadyForWork {
		workersMap.put(occurrence.source, false)
		emit(new NewTick, Scopes.addresses(occurrence.source))
	}

	on AntDiedEvent [!isFromMe(occurrence)] {
		workersMap.remove(occurrence.source)
	}

	def fireNewTick {
		lastTickEmitted = System.currentTimeMillis()
		var evt = new NewTick
		evt.i = 1;
		emit(evt);
	}

	override protected defineFourmiliereParams {
		// Nothing to do with it in this behavior
	}
	
}

/**
 * Agent Fourmi
 * 'Ant' comportement for ACO
 */
agent Fourmi {
	
	uses Logging, Schedules, Lifecycle, DefaultContextInteractions, Behaviors
	
	var currentVertex : TspVertex;
	
	var visitedVertex : List<TspVertex> = new ArrayList();

	var random : Random = new Random();
	
	var totalCost : Double = 0.0;
	
	var isDiying : boolean = false;
	
	var isProcessing : boolean = false; 
	
	var freeToDie : boolean = true;
	
	/** 
	 * Store the parameters available 
	 * for the solver
	 * (Number of ants, etc.)
	 */
	var params : AcoParameters;
	
	on Initialize {
		setLoggingName("Fourmi") 
		currentVertex = occurrence.parameters.get(0) as TspVertex
		params = occurrence.parameters.get(1) as AcoParameters;
		emit(new AntReadyForWork)
	}

	/**
	 * The Ant does not decide when she run
	 * But she inform the Colony when she is done working
	 */
	on NewTick [!this.isFromMe(occurrence) && !isDiying] {
		isProcessing = true;
		var destination = getBestMove
		if (destination != null) {
			var data : AcoTspEdgeData = destination.data as AcoTspEdgeData;
      		var dampened : Double = 0.004 / totalCost;
			data.adjustWeight(dampened, params.phEvaporation);
			currentVertex = destination.to as TspVertex;
			visitedVertex.add(currentVertex)
			totalCost = totalCost + destination.cost; 
			emit(new AntTickOver => [vertex = currentVertex]);
		} else {
			killMySelf 		// She is buggy
		}
		isProcessing = false
	}
	
	/**
	 * Get the best move possible 
	 * for the current Ant
	 * TODO This method does not work perfectly 
	 */
	def getBestMove : Edge<VertexInfo> {
		
		// Get the available Ant moves
		var availableEdgesMoves : List<Edge<VertexInfo>> = new ArrayList();
		for (edge : currentVertex.outgoingEdges) {
			if (!(edge.to.name.equals(currentVertex.name)) && !visitedVertex.contains(edge.to)) {
				availableEdgesMoves.add(edge)
			}
		}

		// Calculate all the probabilities
		var allowedYSum : double = 0d;
		var transitionProbabilities : double[] = newDoubleArrayOfSize(availableEdgesMoves.size);
		var counter : int = 0;
		for (e : availableEdgesMoves) {
			var proba : double = computeNumerator(e);
			transitionProbabilities.set(counter, proba);
			allowedYSum += proba;
			counter++;
		}

		if (allowedYSum == 0d) {
			return null;
		}

		// Add the sub
		var counter2 : int = 0;
		var sum : double = 0d;
		for (e : availableEdgesMoves) {
			var weighted : double = transitionProbabilities.get(counter2) / allowedYSum;
			transitionProbabilities.set(counter2, weighted);
			sum += weighted;
			counter2++;
		}

		// Select the move
		var counter3 : int = 0;
		var random : double = random.nextDouble * sum;
		for (e : availableEdgesMoves) {
			random -= transitionProbabilities.get(counter3);
			if (random <= 0) {
				return e;
			}
			counter3++;
		}
		return null;
	}

	def computeNumerator(e : Edge<VertexInfo>) : Double {
		var to = e.to as TspVertex;
		var weight : Double = currentVertex.cost(to)
		if (weight != 0d) {
			return (FastMath.pow(weight, params.alpha) * FastMath.pow(1d / weight, params.beta));
		}
		return 0d;
	}
	
	on FourmiFinishedRaceEvent [!isDiying] {
		var evt : FourmiFinishedWorking = new FourmiFinishedWorking();
		evt.flow = this.visitedVertex;
		evt.totalCost = totalCost;
		emit(evt);
		killMySelf
	}
	
	on FourmiAskDieEvent [!isDiying && !isFromMe] {
		killMySelf
	}
	
	def killMySelf {
		if (!isDiying) {
			wake(new FourmiAskDieEvent)
		}
	}
	
	/**
	 * Called when wake(new FourmiAskDieEvent)
	 * it will take care of the ant safe suicide
	 * to prevent agent to stay alive
	 * TODO Sometime she can't die, because she is processing? 
	 */
	on FourmiAskDieEvent [isFromMe && !isDiying && freeToDie] {
		freeToDie = false;
		if (isProcessing) {
			in(1000) [
				freeToDie = true;
				if (!isDiying) {
					wake(new FourmiAskDieEvent)
				}
			]
		}
		else {
			isDiying = true;
			emit(new AntDiedEvent);
			in(10000)[
				killMe
			]
		}
	}

	on NewTspProblemParameters [!isDiying] {
		if (occurrence.params != null) {
			params = occurrence.params;
		}
	}

	on StopSolvingEvent [!isDiying] {
		killMySelf
	}
	
}

/**
 * Agent used to pipe message with Java
 * Has the Fourmiliere Behavior 
 */
agent Launcher extends ALauncher {
	
	uses DefaultContextInteractions, Behaviors, Logging, Schedules, Lifecycle
	
	var fourmiliereBehavior : FourmiliereBehavior;
	var solutionObserverBehavior : SolutionObserverBehavior;
	var ticksSync : TickSyncronizer;
	var isDiying : boolean = false;
	
	/** 
	 * Store all the variables
	 * that will require to be triggered
	 * on the new states (the solver, etc.)
	 * ! These should only be Java object
	 * if a SARL require it, use the Events
	 * to benefit of parallelism 
	 */
	var acoListeners = <SarlAcoListener>newArrayList
	
	/** 
	 * Store if the behavior is in testing mode, or not.
	 * This will generally be used to trigger logging
	 */
	var testing : boolean = false;
	
	var tickNumber : int = 0;

	on Initialize {
		solver = occurrence.parameters.get(0) as AntColonyTspSolver;
		testing = occurrence.parameters.get(1) as Boolean;
		
		// Register Both ACO Behavior
		fourmiliereBehavior = new FourmiliereBehavior(this)
		registerBehavior(fourmiliereBehavior);

		solutionObserverBehavior = new SolutionObserverBehavior(this);
		registerBehavior(solutionObserverBehavior);

		ticksSync = new TickSyncronizer(this)
		registerBehavior(ticksSync);
		
		var testEvt = new TriggerTestingEvent
		testEvt.isTesting = testing
		wake(testEvt)
		emit(new NewTspProblemParameters => [params = solver.parameters as AcoParameters]);
		wake(new InternalBehaviorInitialize)
		wake(new ProcessControllerEvent)
		setLoggingName("Launcher")
	}
	
	on SarlAcoProcessUpdate {
		acoListeners.add(occurrence.obs); 
	}

	on NewTick [isFromMe] {
		tickNumber++;
		solver.runLater(new NewTickSentRunnable(acoListeners, tickNumber)); 
	}

	on ProcessControllerEvent[!isDiying] {
		in(50) [ // It loop on himself to handle
			wake(new ProcessControllerEvent); 
		]
	}

	on NewTick {
		fireNewState
	}
	
	override killMyselfSafe {
		isDiying = true; 
		in(10000)[
			killMe
		]
	}

}

class NewTickSentRunnable implements Runnable {

	var listeners : ArrayList<SarlAcoListener>
	var tickNumber : int

	new(listeners : ArrayList<SarlAcoListener>, tickNumber : int) {
		this.listeners = listeners;
		this.tickNumber = tickNumber; 
	}

	override run {
		for (listener : this.listeners) {
			listener.onNewTick(tickNumber) 
		}
	}
	
}
